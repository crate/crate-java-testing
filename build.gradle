plugins {
    id 'idea'
    id 'java'
    id 'maven'
    id 'signing'
    id 'com.github.johnrengelman.shadow' version '1.2.1'
}

description = 'Crate Testing'


group = "io.crate"

archivesBaseName = "crate-testing"

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "junit:junit:$junitVersion"
    compile "io.crate:crate-client:$crateClientVersion"
    compile "org.apache.commons:commons-compress:1.2"

    // workaround in order to properly use ES TransportClient
    compile "com.tdunning:t-digest:$tDigestVersion"

    testCompile ("com.carrotsearch.randomizedtesting:randomizedtesting-runner:$randomizedTestingVersion") {
        exclude group: 'junit', module: 'junit'
    }
}

idea {
    project {
        languageLevel = 'JDK_1_7'

        ipr {
            withXml { provider ->
                def node = provider.asNode()
                def copyrightManager = node.component.find { it.'@name' == 'CopyrightManager' }
                copyrightManager.@default = "CrateASL2"
                def aslCopyright = copyrightManager.copyright.find { it.option.find { it.@name == "myName" }?.@value == "CrateASL2" }
                if (aslCopyright == null) {
                    copyrightManager.append(new XmlParser().parse(file("copyright.xml")))
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}

task getVersion ( dependsOn: 'classes') << {
    ext.gitTag = "git describe".execute().in.text.trim()

    if (!gradle.taskGraph.hasTask(uploadArchives)) {
        version = version + "-" + "git rev-parse --short HEAD".execute().in.text.trim()
    }

    logger.quiet("gitTag: " + gitTag)
    logger.quiet("version: " + version)
}


shadowJar {
    baseName 'crate-testing'
    classifier ''
    duplicatesStrategy 'exclude'
    exclude 'META-INF/**'

    relocate 'io.crate.action',     'io.crate.testserver.action'
    relocate 'io.crate.client',     'io.crate.testserver.client'
    relocate 'io.crate.shade',      'io.crate.testserver.shade'
    relocate 'org.apache.commons',  'io.crate.testserver.apache.commons'
    relocate 'com.tdunning',        'io.crate.testsercer.tdunning'

    doLast {
        manifest {
            attributes("Implementation-Title": "Crate.IO Testing", "Implementation-Version": project.version)
        }
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
    }
    test {
        resources {
            srcDir 'src/test/java'
            include '**/*.tar.gz'
        }
    }
}

task buildJar(dependsOn: [getVersion]) {
    doLast {
        ext.version= project.version
        tasks.shadowJar.execute()
    }
}
tasks.buildJar.mustRunAfter jar // otherwise jar task would override shadowJar artifact

task myJavadocs(type: Javadoc, dependsOn: processResources) {
    classpath = configurations.compile
    source = sourceSets.main.allJava
}

task javadocJar (type: Jar, dependsOn: [myJavadocs]) {
    classifier = 'javadoc'
    from myJavadocs.destinationDir
    manifest {
        attributes("Implementation-Title": "Crate.IO Testing")
    }
}

task buildJavadocJar (dependsOn: [getVersion, myJavadocs] ) << {
    tasks.javadocJar.execute()
}

task sourceJar (type : Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    manifest {
        attributes("Implementation-Title": "Crate.IO Testing")
    }
}

task buildSourceJar (dependsOn: [getVersion] ) << {
    tasks.sourceJar.execute()
}


artifacts {
    archives shadowJar
    archives javadocJar
    archives sourceJar
}

task signJars (type : Sign, dependsOn: [buildJar, buildJavadocJar, buildSourceJar]) {
    sign configurations.archives
}


project.ext.bintrayUsername = project.hasProperty('bintrayUsername') ? bintrayUsername : ""
project.ext.bintrayPassword = project.hasProperty('bintrayPassword') ? bintrayPassword : ""
project.ext.url = 'https://crate.io'
project.ext.scm = {
    url 'https://github.com/crate/crate-java-testing'
    connection 'scm:git:git://github.com/crate/crate-java-testing.git'
    developerConnection 'scm:git:ssh:git@github.com:crate/crate-java-testing.git'
}
project.ext.licenses = {
    license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/license/LICENSE-2.0.txt'
        distribution 'repo'
    }
}
project.ext.developers = {
    developer {
        id 'crate'
        name 'Crate Developers'
        email 'office@crate.io'
    }
}

uploadArchives.dependsOn([signJars])
uploadArchives {
    repositories{
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
                    if (project.ext.bintrayUsername.length() == 0 || project.ext.bintrayPassword.length() == 0) {
                        throw new StopExecutionException("uploadArchives cannot be called without bintray username and password")
                    }
            }

            pom.project {
                artifactId 'crate-testing'
                name 'crate-testing'
                description 'Crate.IO Java Test layer which is used to download and start crate for testing'
                url project.ext.url
                scm project.ext.scm
                licenses project.ext.licenses
                developers project.ext.developers
            }

            pom = addFilter('crate-testing') {artifact, file ->
                artifact.name == 'crate-testing'
            }

            pom.whenConfigured {
                p -> p.dependencies = p.dependencies.findAll {
                    dep -> dep.artifactId != "crate-client"
                }
            }

            repository(id: 'crate-testing', url: 'https://api.bintray.com/maven/crate/crate/crate-testing') {
                authentication(userName: project.ext.bintrayUsername, password: project.ext.bintrayPassword)
            }
        }
    }
}
